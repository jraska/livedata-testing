apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply plugin: 'signing'

version = rootProject.ext.version
group = 'com.jraska.livedata'

android {
  compileSdkVersion 30
  defaultConfig {
    minSdkVersion 19
    targetSdkVersion 30
    versionName version
    versionCode rootProject.ext.versionCode
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

dependencies {
  implementation 'androidx.annotation:annotation:1.3.0'
  implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
  implementation 'androidx.lifecycle:lifecycle-livedata-core:2.4.1'

  testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  testImplementation 'junit:junit:4.13.2'
  testImplementation 'androidx.arch.core:core-testing:2.1.0'
  testImplementation 'org.assertj:assertj-core:3.23.1'
}

task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  archiveClassifier = 'sources'
}

artifacts {
  archives sourcesJar
}

afterEvaluate {
  publishing {
    publications {
      release(MavenPublication) {
        groupId 'com.jraska.livedata'
        artifactId 'testing'
        version rootProject.ext.version

        from components.release
        artifact sourcesJar

        pom {
          name = 'com.jraska.livedata'
          description = 'LiveData Testing'
          url = 'https://github.com/jraska/livedata-testing'
          licenses {
            license {
              name = 'The Apache Software License, Version 2.0'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }
          developers {
            developer {
              id = 'jraska'
              name = 'Josef Raska'
            }
          }

          scm {
            connection = 'scm:git:github.com/jraska/livedata-testing.git'
            developerConnection = 'scm:git:ssh://github.com/jraska/livedata-testing.git'
            url = 'https://github.com/jraska/livedata-testing/tree/main'
          }
        }
      }
    }
  }
}

// Adding these directly into signing {} below didn't work :idk
ext["signing.keyId"] = System.getenv("SIGNING_KEY_ID")
ext["signing.password"] = System.getenv("SIGNING_KEY_PASSWORD")
ext["signing.secretKeyRingFile"] = "$rootDir/gradle/signing-key.gpg"

signing {
  sign publishing.publications
}
